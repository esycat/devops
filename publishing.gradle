import org.ajoberstar.gradle.git.tasks.*

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.ajoberstar', name: 'gradle-git', version: project.ext.plugin.git.version
    }
}

apply plugin: 'maven-publish'
apply plugin: 'scala'

publishing {
    publications {
        main(MavenPublication) {
            from components.java

            artifact source: sourceJar,   classifier: "src"
            artifact source: scaladocJar, classifier: "doc"

            pom.withXml {
                asNode().children().last() + getPomConfiguration()
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            url project.ext.sonatype.url
            credentials {
                username = System.properties['sonatype.username']
                password = System.properties['sonatype.password']
            }
        }
    }
}

task install {
    dependsOn ':publishMainPublicationToMavenLocalRepository'
}

task tagVersion(type: GitTag) {
    tagName = version
    message = "Release of v${version}."
    // sign = true // JGit doesn't support signing yet
    doLast {
        println sprintf('A new version %s has been tagged.', tagName)
    }
}

/**
 * Create POM config and return for use by other tasks.
 */
def getPomConfiguration() {
    def github = project.ext.github
    def projectUrl = sprintf('https://github.com/%s/%s', github.username, github.repository.name)
    def scmConnectionUrl    = sprintf('scm:git:git://github.com/%s/%s.git', github.username, github.repository.name)
    def scmDevConnectionUrl = sprintf('scm:ssh:git@github.com:%s/%s.git', github.username, github.repository.name)

    return {
        name project.name
        description project.description
        url projectUrl
        inceptionYear '2013'
        packaging 'jar'
        licenses {
            license {
                name project.ext.license.name
                url project.ext.license.url
                distribution 'repo'
            }
        }
        scm {
            url projectUrl
            connection scmConnectionUrl
            developerConnection scmDevConnectionUrl
        }
        developers {
            developer {
                id 'esycat'
                name 'Eugene Janusov'
                email 'esycat@gmail.com'
            }
        }
        organization {
            name 'Persic Entertainment'
        }
    }
}
